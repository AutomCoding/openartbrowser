# QIDS: QIDS of artworks
SELECT
?artwork
(CONCAT('[', GROUP_CONCAT(DISTINCT ?class;SEPARATOR=', '), ']') as ?classes)
?artworkLabel
?artworkDescription
?image
(CONCAT('[',GROUP_CONCAT(DISTINCT ?creator;SEPARATOR=', '), ']') as ?creators)
(CONCAT('[',GROUP_CONCAT(DISTINCT ?location;SEPARATOR=', '), ']') as ?locations)
(CONCAT('[',GROUP_CONCAT(DISTINCT ?genre;SEPARATOR=', '), ']') as ?genres)
(CONCAT('[',GROUP_CONCAT(DISTINCT ?movement;SEPARATOR=', '), ']') as ?movements)
(YEAR(xsd:dateTime(?inception)) AS ?inceptionYear)
(CONCAT('[',GROUP_CONCAT(DISTINCT ?material;SEPARATOR=', '), ']') as ?materials)
(CONCAT('[',GROUP_CONCAT(DISTINCT ?depicts;SEPARATOR=', '), ']') as ?depictions)
?countryLabel
?height
?width
WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
  ?class (wdt:P279*) wd:Q3305213 .
  ?artwork wdt:P31 ?class ;
    wdt:P170 ?creator ;
    wdt:P18 ?image .
  FILTER ( ?artwork IN ($QIDS) )
  OPTIONAL { ?artwork  wdt:P276 ?location . }
  OPTIONAL { ?artwork wdt:P136 ?genre . }
  OPTIONAL { ?artwork wdt:P135 ?movement . }
  OPTIONAL { ?artwork wdt:P571 ?inception . }
  OPTIONAL { ?artwork wdt:P186 ?material . }
  OPTIONAL { ?artwork wdt:P180 ?depicts . }
  OPTIONAL { ?artwork wdt:P17 ?country . }
  OPTIONAL { ?artwork wdt:P2048 ?height . }
  OPTIONAL { ?artwork wdt:P2049 ?width . }
  OPTIONAL {
  ?sitelink schema:about ?artwork .
  ?sitelink schema:inLanguage "en" .
  FILTER (SUBSTR(str(?sitelink), 1, 25) = "https://en.wikipedia.org/")
  }

} GROUP BY ?artwork ?artworkLabel ?artworkDescription ?image ?inception ?countryLabel ?height ?width
