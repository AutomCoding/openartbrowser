# QIDS: QIDS of artworks
SELECT
# 1:1 values
?artwork
?artwork_label_en
?artwork_description_en
?wikipedia_link_en
?artwork_label_de
?artwork_description_de
?wikipedia_link_de
?artwork_label_fr
?artwork_description_fr
?wikipedia_link_fr
?artwork_label_it
?artwork_description_it
?wikipedia_link_it
?artwork_label_es
?artwork_description_es
?wikipedia_link_es
# 1:n values
(SAMPLE(?country_EN) AS ?country_en)
(SAMPLE(?country_DE) AS ?country_de)
(SAMPLE(?country_FR) AS ?country_fr)
(SAMPLE(?country_IT) AS ?country_it)
(SAMPLE(?country_ES) AS ?country_es)
(GROUP_CONCAT(DISTINCT ?image;SEPARATOR=', ') as ?images)
(GROUP_CONCAT(DISTINCT ?class;SEPARATOR=', ') as ?classes)
(GROUP_CONCAT(DISTINCT ?creator;SEPARATOR=', ') as ?creators)
(GROUP_CONCAT(DISTINCT ?depicts;SEPARATOR=', ') as ?depictions)
(GROUP_CONCAT(DISTINCT ?genre;SEPARATOR=', ') as ?genres)
(GROUP_CONCAT(DISTINCT ?height;SEPARATOR=', ') as ?heights)
(GROUP_CONCAT(DISTINCT YEAR(xsd:dateTime(?inception));SEPARATOR=', ') as ?inceptions)
(GROUP_CONCAT(DISTINCT ?location;SEPARATOR=', ') as ?locations)
(GROUP_CONCAT(DISTINCT ?movement;SEPARATOR=', ') as ?movements)
(GROUP_CONCAT(DISTINCT ?material;SEPARATOR=', ') as ?materials)
(GROUP_CONCAT(DISTINCT ?width;SEPARATOR=', ') as ?widths)
(GROUP_CONCAT(DISTINCT ?iconclass;SEPARATOR=', ') as ?iconclasses)
WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" .
                          ?artwork rdfs:label ?artwork_label_en .
                          ?artwork schema:description ?artwork_description_en .
                         }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "de".
                          ?artwork rdfs:label ?artwork_label_de .
                          ?artwork schema:description ?artwork_description_de .
                         } hint:Prior hint:runLast false.
  SERVICE wikibase:label { bd:serviceParam wikibase:language "fr".
                          ?artwork rdfs:label ?artwork_label_fr .
                          ?artwork schema:description ?artwork_description_fr .
                         } hint:Prior hint:runLast false.
  SERVICE wikibase:label { bd:serviceParam wikibase:language "it".
                          ?artwork rdfs:label ?artwork_label_it .
                          ?artwork schema:description ?artwork_description_it .
                         } hint:Prior hint:runLast false.
  SERVICE wikibase:label { bd:serviceParam wikibase:language "es".
                          ?artwork rdfs:label ?artwork_label_es .
                          ?artwork schema:description ?artwork_description_es .
                         } hint:Prior hint:runLast false.
  ?class (wdt:P279*) wd:Q3305213 .
  ?artwork wdt:P31 ?class ;
    wdt:P18 ?image .
  FILTER ( ?artwork IN ($QIDS) )
  OPTIONAL {
    ?wikipedia_link_en schema:about ?artwork .
    ?wikipedia_link_en schema:inLanguage "en" .
    ?wikipedia_link_en schema:isPartOf/wikibase:wikiGroup "wikipedia" .
  }
  OPTIONAL {
    ?wikipedia_link_de schema:about ?artwork .
    ?wikipedia_link_de schema:inLanguage "de" .
    ?wikipedia_link_de schema:isPartOf/wikibase:wikiGroup "wikipedia" .
  }
  OPTIONAL {
    ?wikipedia_link_fr schema:about ?artwork .
    ?wikipedia_link_fr schema:inLanguage "fr" .
    ?wikipedia_link_fr schema:isPartOf/wikibase:wikiGroup "wikipedia" .
  }
  OPTIONAL {
    ?wikipedia_link_it schema:about ?artwork .
    ?wikipedia_link_it schema:inLanguage "it" .
    ?wikipedia_link_it schema:isPartOf/wikibase:wikiGroup "wikipedia" .
  }
  OPTIONAL {
    ?wikipedia_link_es schema:about ?artwork .
    ?wikipedia_link_es schema:inLanguage "es" .
    ?wikipedia_link_es schema:isPartOf/wikibase:wikiGroup "wikipedia" .
  }
  OPTIONAL { ?artwork wdt:P170 ?creator . }
  OPTIONAL { ?artwork wdt:P276 ?location . }
  OPTIONAL { ?artwork wdt:P136 ?genre . }
  OPTIONAL { ?artwork wdt:P135 ?movement . }
  OPTIONAL { ?artwork wdt:P571 ?inception . }
  OPTIONAL { ?artwork wdt:P186 ?material . }
  OPTIONAL { ?artwork wdt:P180 ?depicts . }
  OPTIONAL { ?artwork wdt:P17 ?country .
           SERVICE wikibase:label { bd:serviceParam wikibase:language "en" .
                           ?country rdfs:label ?country_EN . }
           SERVICE wikibase:label { bd:serviceParam wikibase:language "de" .
                           ?country rdfs:label ?country_DE . }
           SERVICE wikibase:label { bd:serviceParam wikibase:language "fr" .
                           ?country rdfs:label ?country_FR . }
           SERVICE wikibase:label { bd:serviceParam wikibase:language "it" .
                           ?country rdfs:label ?country_IT . }
           SERVICE wikibase:label { bd:serviceParam wikibase:language "es" .
                           ?country rdfs:label ?country_ES . }
           }
  OPTIONAL { ?artwork wdt:P2048 ?height . }
  OPTIONAL { ?artwork wdt:P2049 ?width . }
  OPTIONAL { ?artwork wdt:P1257 ?iconclass . }
} GROUP BY
?artwork
?artwork_label_en
?artwork_description_en
?wikipedia_link_en
?artwork_label_de
?artwork_description_de
?wikipedia_link_de
?artwork_label_fr
?artwork_description_fr
?wikipedia_link_fr
?artwork_label_it
?artwork_description_it
?wikipedia_link_it
?artwork_label_es
?artwork_description_es
?wikipedia_link_es
